grammar org.mk4h.Puddle with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate puddle "http://www.mk4h.org/Puddle"



Song returns Song:
	(phrases+=Phrase)*
	
	'song' name=STRING '{'
		('key' keyName=PITCH keyType=('major'|'minor'))?
		(timeOverride?='time' timeNum=INT '/' timeDen=INT)?
		(octaveOverride?='octave' baseOctave=SIGNED_INT)?
		(durationOverride?='default' 'duration' defaultDuration=INT)?
		(entries+=Entry)*
	'}'
;

Phrase returns Phrase:
	'phrase' name=ID '{'
		(octaveOverride?='octave' baseOctave=SIGNED_INT)?
		(durationOverride?='default' 'duration' defaultDuration=INT)?
		(entries+=Entry)*
	'}'
;

Entry returns Entry:
	Note | PhraseReference
;

// The explicit whitespace has to be specified after the token differentiating the optional groups
Note returns Note hidden():
	pitch=PITCH (octave=SIGNED_INT)? (WS | ML_COMMENT | SL_COMMENT )+ (duration=INT (WS | ML_COMMENT | SL_COMMENT )*)? (lyrics=STRING)?
;


PhraseReference returns PhraseReference hidden():
	'\\' phrase=[Phrase|ID]
;

// This is clashing with terminal STRING 
//terminal QUOTED_ID: 
//	'"' ID '"'|
//	"'" ID "'"
//;

// https://stackoverflow.com/questions/41700009/xtext-not-accepting-string-constant-expecting-rule-id
//MyID: ID | PITCH;

terminal SIGNED_INT returns ecore::EInt: ('+'|'-')('0'..'9')+;

terminal PITCH: 'c'|'d'|'e'|'f'|'g'|'a'|'h'|'b';
